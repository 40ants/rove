#!/bin/sh
#|-*- mode:lisp -*-|#
#| Run tests with Rove.
exec ros -Q -- $0 "$@"
|#
(progn ;;init forms
  (ros:ensure-asdf)
  (ql:quickload '(:rove) :silent t)

  (when (ros:getenv "COVERALLS")
    (ql:quickload '(:cl-coveralls :split-sequence) :silent t)))

(defpackage :ros.script.roswell/rove.3697685121
  (:use :cl))
(in-package :ros.script.roswell/rove.3697685121)

(defun run-file-tests (file)
  (let ((tries-so-far (make-hash-table :test 'equalp))
        (*error-output* (make-broadcast-stream)))
    (tagbody retry
       (handler-case
           (asdf:test-system
            (cond
              ((equal (pathname-type file) "lisp")
               (let ((*print-case* :downcase))
                 (princ-to-string
                  (second (asdf/package-inferred-system::file-defpackage-form file)))))
              ((equal (pathname-type file) "asd")
               (pathname-name file)))
            :verbose nil)
         (asdf:missing-dependency (c)
           (let ((missing (asdf::missing-requires c)))
             (when (gethash missing tries-so-far)
               (error "Cannot load a dependency ~A, which is required by ~A"
                      missing (asdf::missing-required-by c)))
             (setf (gethash missing tries-so-far) t)
             (ql:quickload missing :silent t)
             (go retry)))))))

(defun main (target &rest argv)
  (declare (ignore argv))
  (let ((file (probe-file target)))
    (unless file
      (format *error-output* "~&Error: ~A not found.~%" target)
      (uiop:quit -1))
    (unless (member (pathname-type file) '("lisp" "asd")
                    :test #'string=)
      (format *error-output* "~&Error: Invalid file type.~%")
      (uiop:quit -1))

    (or #.(if (ros:getenv "COVERALLS")
              `(,(intern (string :with-coveralls) :coveralls)
                (:exclude
                 (,(intern (string :split-sequence) :split-sequence)
                  #\: (or (uiop:getenv "COVERAGE_EXCLUDE") "")
                  :remove-empty-subseqs t))
                (run-file-tests file))
              '(run-file-tests file))
        (uiop:quit -1))))
;;; vim: set ft=lisp lisp:
